buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12.2-1.0.5"
group = "c4.consecration"
archivesBaseName = "consecration"

repositories {
    maven {
        name = "Tinkers"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }

}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "stable_39"
}

dependencies {
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.1.21"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.10.1.87"
    deobfCompile "adventurers-toolbox:toolbox:0.2.11"
    deobfCompile "spartan-weaponry:SpartanWeaponry-1.12.2:beta:1.2.3"
    deobfCompile "metamorph:metamorph:1.1.6:1.12.2"
    deobfCompile "constructs-armory:conarm:1.12.2:1.2.1"
    deobfCompile "mk-ultra:MKUltra:0.80"
    deobfCompile "targetingapi:targeting_api:0.12"
    deobfCompile "tetra:tetra:1.12.2:0.26.0"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    onlyIf {
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar
